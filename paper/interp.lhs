%if style == newcode
\begin{code}
{-# LANGUAGE FunctionalDependencies #-}
{-# LANGUAGE DerivingStrategies #-}
import Data.Map (Map)
import qualified Data.Map as Map
import Data.Set (Set)
import qualified Data.Set as Set
import Data.List (find, foldl')
import Data.Function (fix)
import Control.Monad
import Control.Monad.Trans.State
import Expr

main = eval @_ @(Value (ByName T))`seq` return ()

deriving instance Show Event
instance Show a => Show (T a) where
  show (Step e t) = show e ++ "‚Üí" ++ show t
  show (Ret a) = '‚ü®':show a++"‚ü©"
instance Show (Value œÑ) where
  show (Fun _) = "Œª"
  show (Con k _) = show k ++ "(...)"
  show Stuck = "üó≤"
instance (Show (œÑ v)) => Show (ByName œÑ v) where
  show (ByName œÑ) = show œÑ

data Div = Div
instance Show Div where
  show Div = "..."

takeT :: Int -> T a -> T (Either Div a)
takeT 0 _ = return (Left Div)
takeT _ (Ret a) = return (Right a)
takeT n (Step e t) = Step e (takeT (n-1) t)
\end{code}
%endif

\section{A Denotational Interpreter}
\label{sec:interp}

We will now define a denotational interpreter in Haskell.
It is worth stressing that we picked Haskell out of convenience and familiarity,
rather than out of necessity:
We make use of thunks in only one key position where memoisation is not
important and could just as well have used a strict higher-order language such
as OCaml, ML or Scheme with explicit suspension @fun () -> _@.

\begin{figure}
\begin{spec}
type Name = String
type Tag = Int; conArity :: Tag -> Int
data Expr  = Var Name | App Expr Name | Lam Name Expr | Let Name Expr Expr
           | ConApp Tag [Name] | Case Expr [Alt]
type Alt = (Tag,[Name],Expr)
\end{spec}
\caption{Syntax}
\label{fig:syntax}
\end{figure}

\subsection{Semantic Domain}
A denotational interpreter is both a definitional interpreter as well as a
denotational semantics.
Then what is its \emph{semantic domain}?
To a first approximation, we can think of it as a type |D|, defined as
\begin{minipage}{0.65\textwidth}
\begin{comment}
\begin{code}
type D œÑ = œÑ (Value œÑ)
data T a = Step Event (T a) | Ret a
data Event  = Lookup Name | Update | App1 | App2
            | Bind | Case1 | Case2
data Value œÑ = Stuck | Fun (D œÑ -> D œÑ) | Con Tag [D œÑ]
\end{code}
\end{comment}
\begin{spec}
type D = T Value
data T a = Step Event (T a) | Ret a
data Event  = Lookup Name | Update | App1 | App2
            | Bind | Case1 | Case2
data Value = Stuck | Fun (D -> D) | Con Tag [D]
\end{spec}
\end{minipage}
\begin{minipage}{0.35\textwidth}
\begin{spec}
instance Monad T where
  return a = Ret a
  Ret a >>= k = k a
  Step e œÑ >>= k = Step e (œÑ >>= k)
\end{spec}
\begin{comment}
\begin{code}
instance Functor T where
  fmap f (Ret a) = Ret (f a)
  fmap f (Step e t) = Step e (fmap f t)
instance Applicative T where
  pure = Ret
  (<*>) = ap
instance Monad T where
  Ret a >>= k = k a
  Step e œÑ >>= k = Step e (œÑ >>= k)
\end{code}
\end{comment}
\end{minipage}
\noindent
Every such |D| corresponds to a program trace |T| that ends with a concrete
|Value|.
A trace |T| can either |Ret|urn or it can make another |Step|,
indicating that the program makes another small-step transition before reaching
a terminal state.

We embellished each |Step| with intensional information about the particular
type of small-step |Event|, for example we attach the |Name| of the let-bound
variable to |Lookup|.
The reason for this decision will become clear later on; just note that the
choice of |Event| suggests a spectrum of intensionality, with |data Event =
Unit| corresponding to the ``delay monad'' popularised by \citet{Capretta:05} on
the more abstract end of the spectrum and arbitrary syntactic detail attached to
each of |Event|'s constructors at the other end of the spectrum.
If our language had facilities for input/output, we could have started from a
more elaborate construction such as interaction trees~\citep{interaction-trees}.

The coinductive nature of |T|'s definition in Haskell is crucial to our
approach because it allows us to express diverging traces as an infinite,
productive nesting of |Step|s; in a strict language, we would have introduced
an explicit thunk in the definition of |Step|, \eg, @Step of event * 'a t Lazy.t@.
The |Monad| instance of |T| implements the bind operator |(>>=)| by forwarding
|Step|s, thus guarding the recursion~\citep{Capretta:05}.

A semantic element |D| eventually terminates with a |Value| that is either
|Stuck|, a |Fun|ction waiting to be applied to an argument |D| to yield
one of the same, or a |Con|structor application giving the denotations of its
fields.
|Value| is a standard denotational encoding of its syntactic counterpart, devoid
of any syntax.
(We repress worries about well-definedness and totality to \Cref{sec:adequacy}.)

\begin{figure}
\begin{code}
type (:->) = Map
empty :: Ord k => k :-> v
ext :: Ord k => (k :-> v) -> k -> v -> (k :-> v)
exts :: Ord k => (k :-> v) -> [k] -> [v] -> (k :-> v)
(!) :: Ord k => (k :-> v) -> k -> v
dom :: Ord k => (k :-> v) -> Set k
(‚àà) :: Name -> Set Name -> Bool
\end{code}
\begin{comment}
\begin{code}
empty = Map.empty
ext œÅ x d = Map.insert x d œÅ
exts œÅ xs ds = foldl' (uncurry . ext) œÅ (zip xs ds)
(!) = (Map.!)
dom = Map.keysSet
(‚àà) = Set.member
\end{code}
\end{comment}
\caption{Environments}
\label{fig:map}
\end{figure}

\begin{figure}
\begin{minipage}{0.55\textwidth}
\begin{code}
eval  ::  (IsTrace œÑ, IsValue œÑ v, HasAlloc œÑ v)
      =>  Expr -> (Name :-> œÑ v) -> œÑ v
eval e œÅ = case e of
  Var x    | x ‚àà dom œÅ  -> œÅ ! x
           | otherwise  -> retStuck
  App e x  | x ‚àà dom œÅ  -> step App1 $ do
               v <- eval e œÅ
               apply v (œÅ ! x)
           | otherwise  -> retStuck
  Lam x e -> retFun $ \d ->
    step App2 (eval e ((ext œÅ x d)))
  Let x e1 e2 -> do
    d1 <- alloc (\d1 -> eval e1 (ext œÅ x (step (Lookup x) d1)))
    step Bind (eval e2 (ext œÅ x (step (Lookup x) d1)))
  ConApp k xs
    | all (‚àà dom œÅ) xs, length xs == conArity k
    -> retCon k (map (œÅ !) xs)
    | otherwise
    -> retStuck
  Case e alts -> step Case1 $ do
    v <- eval e œÅ
    select v [ (k, cont er xs) | (k,xs,er) <- alts ]
    where
       cont er xs ds
         | length xs == length ds
         = step Case2 (eval er (exts œÅ xs ds))
         | otherwise
         = retStuck
\end{code}
%  ConApp k xs  | all (‚àà dom œÅ) xs  -> retCon k (map (œÅ !) xs)
%               | otherwise         -> retStuck
%  Case e alts -> step Case1 (eval e œÅ >>= \v ->
%    select v [ (k, step Case2 . eval er . exts œÅ xs) | (k,xs,er) <- alts ])
\end{minipage}%
\begin{minipage}{0.44\textwidth}
\begin{code}
class Monad œÑ => IsTrace œÑ where
  step :: Event -> œÑ v -> œÑ v

class IsValue œÑ v | v -> œÑ where
  retStuck :: œÑ v
  retFun :: (œÑ v -> œÑ v) -> œÑ v
  apply :: v -> œÑ v -> œÑ v
  retCon :: Tag -> [œÑ v] -> œÑ v
  select :: v -> [(Tag, [œÑ v] -> œÑ v)] ->  œÑ v

class HasAlloc œÑ v | v -> œÑ where
  alloc :: (œÑ v -> œÑ v) -> œÑ (œÑ v)
\end{code}
\subcaption{Final encoding of traces and values}
  \label{fig:trace-classes}
%if style /= newcode
\begin{code}
instance IsTrace T where
  step = Step

instance IsValue T Value where
  retStuck = return Stuck
  retFun f = return (Fun f)
  retCon k ds = return (Con k ds)
  apply  (Fun f)  d  = f d
  apply  _        _  = retStuck
  select v alts = ...

instance HasAlloc T Value where
  alloc f = return (fix f)
\end{code}
%else
\begin{code}
instance IsTrace T where
  step = Step

instance IsTrace œÑ => IsValue œÑ (Value œÑ) where
  retStuck = return Stuck
  retFun f = return (Fun f)
  retCon k ds = return (Con k ds)
  apply (Fun f) d = f d
  apply _       _ = retStuck
  select v alts
    | Con k ds <- v
    , Just (_,alt) <- find (\(k',_) -> k' == k) alts
    = alt ds
    | otherwise
    = retStuck
\end{code}
%endif
\subcaption{Concrete by-name semantics for |D|}
  \label{fig:trace-instances}
\end{minipage}%
\caption{Abstract Denotational Interpreter}
  \label{fig:eval}
\end{figure}

\subsection{The Interpreter}

We will now use |D| to give meaning to an expression |e| via an interpreter
function |eval :: Expr -> (Name :-> D) -> D|, where the variable environment
|Name :-> D| is simply a finite mapping from free variables of |e| to their
meaning in |D|.
We summarise the API of environments and sets in \Cref{fig:map}.

We give a definition for |eval| in \Cref{fig:eval}, although in the spirit of
abstract definitional interpreters such as in \citet{Keidel:18} its type is
quite a bit more general than its instantiation at |D|.

In particular, the interpreter maps expressions not into a concrete,
\emph{initial} encoding of a trace as an algebraic data type, but into a
fold-like \emph{final encoding}~\citep{Carette:07} thereof, in terms
of three type classes |IsTrace|,|IsValue| and |HasAlloc| depicted in
\Cref{fig:trace-classes}.
%
%TODO: Related Work
%This approach evokes memories of~\citet{Carette:07} because we effectively
%encode expressions as a fold, but our semantic domain |D| of traces is quite
%different because it gives a proper account of diverging traces and is total.
%
Each of these offer knobs that we will tweak individually in later Sections.
|T|races and |Value|s are instances of these type classes via
\Cref{fig:trace-instances}, so |D| can stand in as a |œÑ v| for |eval|.
For example, we can evaluate the expression $\Let{i}{\Lam{x}{x}}{i~i}$ like
this:%
\footnote{where we use |read :: String -> Expr| as a parsing function}

< ghci> eval (read "let i = Œªx.x in i i") empty :: D

%options ghci -pgmL lhs2TeX -optL--pre
\perform{eval (read "let i = Œªx.x in i i") empty :: D (ByName T)}

Which is in direct correspondence to the call-by-name small-step trace
\[\begin{array}{c}
  \arraycolsep2pt
  \begin{array}{clclcl}
             & (\Let{i}{\Lam{x}{x}}{i~i}, [], [], \StopF) & \smallstep[\BindT] & (i~i, œÅ_1, Œº, \StopF)
             & \smallstep[\AppIT] & (i, œÅ_1, Œº, \ApplyF(\pa_1) \pushF \StopF)
             \\
  \smallstep[\LookupT] & (\Lam{x}{x}, œÅ_1, Œº, \ApplyF(\pa_1) \pushF \StopF) & \smallstep[\AppET] & (x, œÅ_2, Œº, \StopF) & \smallstep[\LookupT] & (\Lam{x}{x}, œÅ_1, Œº, \StopF)
  \end{array} \\
  \\[-0.5em]
  \quad \text{where} \quad \begin{array}{lll}
    œÅ_1 = [i ‚Ü¶ \pa_1] & œÅ_2 = [i ‚Ü¶ \pa_1, x ‚Ü¶ \pa_1] & Œº = [\pa_1 ‚Ü¶ (œÅ_1,\Lam{x}{x})]. \\
  \end{array}
\end{array}\]
\noindent
While |IsTrace| is exactly a final encoding of |T|, |IsValue| is not quite the
same to |Value|:
For one, the ``injections'' |retStuck|, |retFun| and |retCon| return a
|D = T Value|, not simply a |Value|; a curiosity that we will revisit in
\Cref{fig:abstractions} when we consider abstract interpretations of |Value|
that don't necessarily instantiate these methods with |return . _|.
On the other hand, the ``eliminators'' |apply| and |select| can be implemented
in the obvious way for |T|.
The omitted definition for |select| finds the |alt| in |alts| that matches the
|Tag| of the |Con| value |v| and applies said |alt| to the field denotations of
|v|; failure to perform any of these steps results in |retStuck|.%
\footnote{We extract from this document a runnable Haskell file which we add as
a Supplement, containing the complete definitions.}

The third type class is |HasAlloc|, a most significant knob to our
interpreter because it fixes a particular evaluation strategy.
We will play with this knob in \Cref{sec:evaluation-strategies}.
Like |IsValue|, it is parameterised both over the type of values
\emph{as well as} the type of trace, where the syntax | || v -> œÑ| is a
functional dependency indicating that the type of |v| completely determines |œÑ|.
In other words: The choice of value will always be specific to a particular type
of trace, the same way that |Value| hard-codes its use of |T|.

The |alloc| method of |HasAlloc| is used to give meaning to recursive let
bindings; as such its type is \emph{almost} an instance of the venerable least
fixpoint combinator |fix :: (a -> a) -> a|, if it weren't for the additional |œÑ|
wrapping in its result type.
The concrete implementation for |D| given in \Cref{fig:trace-instances} simply
defers to |fix|, yielding a call-by-name evaluation strategy.
We will shortly see examples of eager evaluation strategies that will make some
|step|s in |alloc| instead of simply |return|ing.

%TODO: Move to later section?
Evidently, |eval| is defined by recursion on the structure of |e|.
But every recursive call is also guarded by a call to |step|, so it also
corecursively generates what \Cref{sec:adequacy} will prove to be an adequate
proxy for a small-step trace.

We conclude this Subsection with a few examples, starting with two programs that
diverge. The corecursive formulation allows us to observe finite prefixes of the
trace:

< ghci> takeT 3 $ eval (read "let x = x in x") empty :: D
%options ghci -pgmL lhs2TeX -optL--pre
\perform{takeT 3 $ eval (read "let x = x in x") empty :: D (ByName T)}

< ghci> takeT 3 $ eval (read "let w = Œªy. y y in w w") empty :: D
%options ghci -pgmL lhs2TeX -optL--pre
\perform{takeT 3 $ eval (read "let w = Œªy. y y in w w") empty :: D (ByName T)}

\noindent
And data types work as well, allowing for interesting ways to get stuck:

< ghci> takeT 3 $ eval (read "let z = Z() in let o = S(z) in case o of { S(zz) -> zz }") empty :: D
%options ghci -pgmL lhs2TeX -optL--pre
\perform{takeT 3 $ eval (read "let z = Z() in let o = S(z) in case o of { S(zz) -> zz }") empty :: D (ByName T)}

< ghci> takeT 3 $ eval (read "let z = Z() in z z") empty :: D
%options ghci -pgmL lhs2TeX -optL--pre
\perform{takeT 3 $ eval (read "let z = Z() in z z") empty :: D (ByName T)}

\begin{figure}
\begin{spec}
data ByName œÑ v = ByName (œÑ v)
instance Monad œÑ => Monad (ByName œÑ) where ...
instance IsTrace œÑ => IsTrace (ByName œÑ) where ...
instance Monad œÑ => HasAlloc (ByName œÑ) (Value (ByName œÑ)) where
  alloc f = return (fix f)
\end{spec}
\begin{comment}
\begin{code}
newtype ByName œÑ v = ByName { unByName :: (œÑ v) }
  deriving newtype (Functor,Applicative,Monad)

instance IsTrace œÑ => IsTrace (ByName œÑ) where
  step e = ByName . step e . unByName

instance Monad œÑ => HasAlloc (ByName œÑ) (Value (ByName œÑ)) where
  alloc f = return (fix f)
\end{code}
\end{comment}
\caption{Call-by-name}
\label{fig:by-name}
\end{figure}

\begin{figure}
\begin{spec}
type Addr = Int; type Heap œÑ = Addr :-> D œÑ; nextFree :: Heap œÑ -> Addr
data ByNeed œÑ v = ByNeed (StateT (Heap (ByNeed œÑ)) œÑ v)
instance Monad œÑ => Monad (ByNeed œÑ) where ...

instance IsTrace œÑ => IsTrace (ByNeed œÑ) where
  step e (ByNeed (StateT m)) = ByNeed $ StateT $ \Œº -> step e (m Œº)

fetch :: Monad œÑ => Addr -> D (ByNeed œÑ)
fetch a = ByNeed get >>= \Œº -> Œº ! a

memo :: IsTrace œÑ => Addr -> D (ByNeed œÑ) -> D (ByNeed œÑ)
memo a d = d >>= step Update . ByNeed . StateT . upd
  where upd v Œº = return (v, ext Œº a (memo a (return v)))

instance IsTrace œÑ => HasAlloc (ByNeed œÑ) (Value (ByNeed œÑ)) where
  alloc f = ByNeed $ StateT $ \Œº -> do
    let a = nextFree Œº
    return (fetch a, ext Œº a (memo a (f (fetch a))))
\end{spec}
\begin{comment}
\begin{code}
type Addr = Int; type Heap œÑ = Addr :-> D œÑ; nextFree :: Heap œÑ -> Addr

nextFree h = case Map.lookupMax h of
  Nothing     -> 0
  Just (k,_)  -> k+1

newtype ByNeed œÑ v = ByNeed (StateT (Heap (ByNeed œÑ)) œÑ v)
  deriving newtype (Functor,Applicative,Monad)

instance IsTrace œÑ => IsTrace (ByNeed œÑ) where
  step e (ByNeed (StateT m)) = ByNeed $ StateT $ \Œº -> step e (m Œº)

fetch :: Monad œÑ => Addr -> D (ByNeed œÑ)
fetch a = ByNeed get >>= \Œº -> Œº ! a

memo :: IsTrace œÑ => Addr -> D (ByNeed œÑ) -> D (ByNeed œÑ)
memo a d = d >>= step Update . ByNeed . StateT . upd
  where upd v Œº = return (v, ext Œº a (memo a (return v)))

instance IsTrace œÑ => HasAlloc (ByNeed œÑ) (Value (ByNeed œÑ)) where
  alloc f = ByNeed $ StateT $ \Œº -> do
    let a = nextFree Œº
    return (fetch a, ext Œº a (memo a (f (fetch a))))
\end{code}
\end{comment}
\caption{Call-by-need}
\label{fig:by-need}
\end{figure}

\subsection{More Evaluation Strategies}
\label{sec:evaluation-strategies}

By varying the |HasAlloc| instance of our type |D|, we can endow our language
|Expr| with different evaluation strategies.
With a bit of generalisation, variations become as simple as switching out a
monad transformer, a common phenomenon in abstract definitional
interpreters~\citep{adi}.
Thus we parameterise |D| and |Value| over the particular trace type |T|:
\begin{spec}
type D œÑ = œÑ (Value œÑ)
data Value œÑ = Stuck | Fun (D œÑ -> D œÑ) | Con Tag [D œÑ]
instance IsTrace œÑ => IsValue œÑ (Value œÑ) where ...
\end{spec}

\noindent
\subsubsection{Call-by-name}

We redefine by-name semantics via the |ByName| \emph{trace transformer}
in \Cref{fig:by-name}%
\footnote{The Supplement defines these datatypes as |newtype|s.},
so called because |ByName œÑ| inherits its |Monad| and |IsTrace|
instance from |œÑ| (busywork we omit).
Our old |D| can be recovered as |D (ByName T)|.

\subsubsection{Call-by-need}

By introducing a heap and memoisation to |œÑ|, yielding
a trace transformer |ByNeed| in \Cref{fig:by-need} that encodes a call-by-need
evaluation strategy.
What is interesting is that |ByNeed T| denotes a \emph{stateful function
returning a trace}, thus it is the first time we compute with something
different than a |T|.
The |alloc| implementation for |ByNeed| traces will return a |D| that |fetch|es
the bound action from the heap, under an address |a| that is not taken yet in
the current heap |Œº|.
This newly heap-bound |D| in turn evaluates |f ...|, the result of which is then
|memo|ised in an |Update| step, so that the heap-bound |D| is replaced by
one that immediately |return|s the value.
We have proven this semantics adequate \wrt to the LK transition system in
\Cref{fig:lk-semantics} with the techniques we develop in \Cref{sec:adequacy}.

\todo{Mention that we could also have tested this result, with a type like
|type ValidateT œÑ = StateT œÉ œÑ|, where the |step| action crashes if the |Event|
does not match the currently applicable LK transition.}

It is worth stressing how simple it was to carry out this extension.
Furthermore, nothing here is specific to |Expr| or |Value|!
The only requirement on |T| is that its |Event| type has an |Update| action,
which makes the implementation of |ByNeed| easily reusable.
\sg{That's not completely true; one would still need to duplicate the code to
adjust it to the particular |Event| data type.
That could be worked around with by parameterising |IsTrace œÑ Œµ| over the event
type and then have type class constraint |HasUpdateEvent Œµ| in |HasAlloc|,
but that seems like a lot complexity for such little benefit.}

Example evaluating $\Let{i}{(\Lam{y}{\Lam{x}{x}})~i}{i~i}$:

< ghci> eval (read "let i = (Œªy.Œªx.x) i in i i") empty :: D (ByNeed T)
%options ghci -pgmL lhs2TeX -optL--pre
\perform{eval (read "let i = (Œªy.Œªx.x) i in i i") empty :: D (ByNeed T)}

